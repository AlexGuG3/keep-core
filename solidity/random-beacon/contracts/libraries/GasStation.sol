// SPDX-License-Identifier: MIT
//
// ▓▓▌ ▓▓ ▐▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▄
// ▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▌▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//   ▓▓▓▓▓▓    ▓▓▓▓▓▓▓▀    ▐▓▓▓▓▓▓    ▐▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓
//   ▓▓▓▓▓▓▄▄▓▓▓▓▓▓▓▀      ▐▓▓▓▓▓▓▄▄▄▄         ▓▓▓▓▓▓▄▄▄▄         ▐▓▓▓▓▓▌   ▐▓▓▓▓▓▓
//   ▓▓▓▓▓▓▓▓▓▓▓▓▓▀        ▐▓▓▓▓▓▓▓▓▓▓         ▓▓▓▓▓▓▓▓▓▓         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
//   ▓▓▓▓▓▓▀▀▓▓▓▓▓▓▄       ▐▓▓▓▓▓▓▀▀▀▀         ▓▓▓▓▓▓▀▀▀▀         ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▀
//   ▓▓▓▓▓▓   ▀▓▓▓▓▓▓▄     ▐▓▓▓▓▓▓     ▓▓▓▓▓   ▓▓▓▓▓▓     ▓▓▓▓▓   ▐▓▓▓▓▓▌
// ▓▓▓▓▓▓▓▓▓▓ █▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓
// ▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓ ▐▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓ ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓  ▓▓▓▓▓▓▓▓▓▓
//
//                           Trust math, not hardware.

pragma solidity ^0.8.6;

library GasStation {
    struct Data {
        // Map holding info about all deposits: address => (slot => value).
        mapping(address => mapping(uint256 => uint256)) gasDeposits;
    }

    /// @notice Determines the number of slots which should be used upon
    ///         making a gas deposit.
    /// @dev If the client code requires to reserve more gas, this value
    ///      should be adjusted accordingly. Bear in mind a higher value means
    ///      that transactions making the deposits will be more expensive. In
    ///      return, transactions releasing deposits will receive bigger
    ///      gas refunds.
    uint256 public constant depositSize = 1;

    /// @notice Creates a gas deposit for given address.
    /// @dev Fills up the given number of memory slots with a non-zero value and
    ///      consumes gas as result. That gas can be recovered during release.
    /// @param addr Address which the deposit is tied to.
    function depositGas(Data storage self, address addr) internal {
        setDeposit(self, addr, 1);
    }

    /// @notice Releases a gas deposit for given address.
    /// @dev Sets all slots which have been filled up during deposit to their
    ///      zero values. That action causes a gas refund so the gas spent
    ///      for deposit is recovered.
    /// @param addr Address which the deposit is tied to.
    function releaseGas(Data storage self, address addr) internal {
        setDeposit(self, addr, 0);
    }

    /// @notice Sets the memory slots tied to the given address to the value
    ///         passed as function argument.
    /// @param addr Address which the deposit is tied to.
    /// @param value Value which should be inserted to the memory slots.
    function setDeposit(
        Data storage self,
        address addr,
        uint256 value
    ) internal {
        for (uint256 i = 0; i < depositSize; i++) {
            self.gasDeposits[addr][i] = value;
        }
    }
}
